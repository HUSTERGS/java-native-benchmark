plugins {
    id 'java'
    id "me.champeau.jmh" version "0.7.3"
}

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.bytedeco:javacpp:1.5.11'
    runtimeOnly 'org.bytedeco:javacpp-platform:1.5.11'

    implementation 'net.java.dev.jna:jna-platform:5.17.0'
    implementation 'com.github.jnr:jnr-ffi:2.2.17'
}

jmh {
    fork = 0
    threads = 1
    jvmArgs = ['-Djmh.separateClasspathJAR=true', '--enable-preview', '--enable-native-access=ALL-UNNAMED' ]
    version = '1.37'
}

// Configure Java compilation tasks to enable feature previews
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("--enable-preview")
    javaCompiler.set(javaToolchains.compilerFor {
        languageVersion.set(JavaLanguageVersion.of(24))
    })
}

// Then configure the JMH Tasks to build with Java 15
tasks.withType(me.champeau.jmh.WithJavaToolchain).configureEach {
    javaLauncher.set(javaToolchains.launcherFor {
        languageVersion.set(JavaLanguageVersion.of(24))
    })
}

// Configure the JMH bytecode generator to support feature previews
tasks.withType(me.champeau.jmh.JmhBytecodeGeneratorTask).configureEach {
    jvmArgs.set(["--enable-preview"])
}